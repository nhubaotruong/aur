# Maintainer:  Caleb Maclennan <caleb@alerque.com>
# Contributor: Evan Pitstick <nerdx00 at gmail dot com>
# Contributor: Chet Gray <chetgray@gmail.com>
# Contributor: joni <kljohann@gmail.com>
# Contributor: Dieter Plaetinck <dieter@plaetinck.be>

pkgname=vcsh
pkgver=2.0.0beta1
_pkgver=2.0.0-beta.1
pkgrel=2
pkgdesc='Version Control System for $HOME that manages multiple Git repositories'
arch=(any)
url="https://github.com/RichiH/$pkgname"
license=(GPL2)
depends=(git)
makedepends=(ruby-ronn)
checkdepends=(perl perl-shell-command perl-test-most)
source=("$url/releases/download/v$_pkgver/vcsh-$_pkgver.tar.xz")
sha256sums=('dd766715b6d525509f7bc11d861771cacad29e9661f7b4d0ae5c8a825a35281a')

build() {
	cd "$pkgname-$_pkgver"
	./configure \
		--prefix="/usr" \
		--with-deployment="arch.$pkgrel"
	make
}

check() {
	cd "$pkgname-$_pkgver"
	make test
}

package() {
	cd "$pkgname-$_pkgver"
	make DESTDIR="$pkgdir/" install
}
